{
  config,
  lib,
  pkgs,
  ...
}:

let
  cfg = config.services.anything-llm-oci;
  domain = config.networking.domain;
  subdomain = cfg.reverseProxy.subdomain;
  fqdn = if (subdomain != "") then "${subdomain}.${domain}" else domain;
  internalPort = "3001";

  environment = cfg.environment // {
    UID = "1000";
    GID = "1000";
    SERVER_PORT = internalPort;
    STORAGE_DIR = "/app/server/storage";
  };

  inherit (lib)
    mkDefault
    mkEnableOption
    mkIf
    mkOption
    optional
    types
    ;
in
{
  options.services.anything-llm-oci = {
    enable = mkEnableOption "AnythingLLM container with Podman.";
    port = mkOption {
      type = types.port;
      default = 3001;
      description = "Which port the AnythingLLM server listens to.";
    };
    environment = mkOption {
      default = { };
      type = types.attrsOf types.str;
      description = ''
        Extra environment variables for AnythingLLM.
        For all available options, see <https://github.com/Mintplex-Labs/anything-llm/blob/master/docker/.env.example>
      '';
    };
    environmentFile = mkOption {
      type = types.nullOr types.path;
      default = null;
      example = "config.sops.templates.anything-llm-env.path";
      description = ''
        Environment file to be passed to the AnythingLLM container.
        You should at least provide:
          - JWT_SECRET
          - SIG_KEY
          - SIG_SALT
      '';
    };
    reverseProxy = {
      enable = mkEnableOption "Nginx reverse proxy for AnythingLLM.";
      subdomain = mkOption {
        type = types.str;
        default = "llm";
        description = "Subdomain for Nginx virtual host. Leave empty for root domain.";
      };
      forceSSL = mkOption {
        type = types.bool;
        default = true;
        description = "Force SSL for Nginx virtual host.";
      };
    };
  };

  config = mkIf cfg.enable {
    services.nginx.virtualHosts."${fqdn}" = mkIf cfg.reverseProxy.enable {
      enableACME = cfg.reverseProxy.forceSSL;
      forceSSL = cfg.reverseProxy.forceSSL;
      locations."/" = {
        proxyPass = mkDefault "http://127.0.0.1:${toString cfg.port}";
        proxyWebsockets = true;
      };
    };

    virtualisation.podman = {
      enable = true;
      autoPrune.enable = true;
      dockerCompat = true;
    };

    networking.firewall.interfaces =
      let
        matchAll = if !config.networking.nftables.enable then "podman+" else "podman*";
      in
      {
        "${matchAll}".allowedUDPPorts = [ 53 ];
      };

    virtualisation.oci-containers.backend = "podman";

    virtualisation.oci-containers.containers.anything-llm = {
      inherit environment;
      image = "docker.io/mintplexlabs/anythingllm:latest";
      environmentFiles = optional (cfg.environmentFile != null) cfg.environmentFile;
      volumes = [
        "anything-llm_collector_hotdir:/app/collector/hotdir:rw"
        "anything-llm_collector_outputs:/app/collector/outputs:rw"
        "anything-llm_server_storage:/app/server/storage:rw"
      ];
      ports = [
        "${toString cfg.port}:${internalPort}/tcp"
      ];
      user = with environment; "${UID}:${GID}";
      log-driver = "journald";
      extraOptions = [
        "--add-host=host.docker.internal:host-gateway"
        "--cap-add=SYS_ADMIN"
        "--network-alias=anything-llm"
        "--network=anything-llm_anything-llm"
      ];
    };
    systemd.services."podman-anything-llm" = {
      serviceConfig = {
        Restart = lib.mkOverride 90 "no";
      };
      after = [
        "podman-network-anything-llm_anything-llm.service"
      ];
      requires = [
        "podman-network-anything-llm_anything-llm.service"
      ];
      partOf = [
        "podman-compose-anything-llm-root.target"
      ];
      wantedBy = [
        "podman-compose-anything-llm-root.target"
      ];
    };

    systemd.services."podman-network-anything-llm_anything-llm" = {
      path = [ pkgs.podman ];
      serviceConfig = {
        Type = "oneshot";
        RemainAfterExit = true;
        ExecStop = "podman network rm -f anything-llm_anything-llm";
      };
      script = ''
        podman network inspect anything-llm_anything-llm || podman network create anything-llm_anything-llm --driver=bridge
      '';
      partOf = [ "podman-compose-anything-llm-root.target" ];
      wantedBy = [ "podman-compose-anything-llm-root.target" ];
    };

    systemd.targets."podman-compose-anything-llm-root" = {
      unitConfig = {
        Description = "Root target generated by compose2nix.";
      };
      wantedBy = [ "multi-user.target" ];
    };
  };
}
